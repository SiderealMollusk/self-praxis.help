# Remote Hosts (Override these in .env)
# default: assuming you have ssh config aliases set up
set dotenv-load

nuc_host := env_var_or_default("nuc_host", "nuc")
vps_host := env_var_or_default("vps_host", "vps")

# Project Paths on Remote
nuc_root := "/srv/selfpraxis"
vps_upstream_conf := "/etc/nginx/snippets/selfpraxis-prod-upstream.conf"

set shell := ["bash", "-c"]

# Default: List commands
default:
    @just --list --unsorted

# --- Local Helpers ---

# Check which color is currently active (run from laptop, queries VPS)
# --- Status & Verification ---
# --- Status & Verification ---
active-color:
    @./scripts/dev/get-active-color.sh {{vps_host}}

verify-next:
    @echo "Verifying the Candidate (Inactive) deployment..."
    @INACTIVE=$(./scripts/dev/get-inactive-color.sh {{vps_host}}) && \
    if [ "$INACTIVE" == "green" ]; then \
        echo "Candidate: GREEN (Port 8002)"; \
        curl -I http://100.104.75.126:8002; \
    elif [ "$$INACTIVE" == "blue" ]; then \
        echo "Candidate: BLUE (Port 8000)"; \
        curl -I http://100.104.75.126:8000; \
    fi

# --- Deployment Workflows ---

# --- Secrets Management ---

# Encrypt local .env to secrets.enc.env
save-secrets:
    cd ops && sops --encrypt .env > secrets.enc.env

# Decrypt secrets.enc.env to local .env
load-secrets:
    cd ops && sops --decrypt secrets.enc.env > .env

# Push local .env to remote (Usage: just push-secrets [staging|prod])
push-secrets env:
    @echo "Pushing secrets to {{env}}..."
    scp ops/.env {{nuc_host}}:{{nuc_root}}/{{env}}/ops/.env

# --- Deployment Workflows ---


# Deploy to Staging
deploy-staging:
    @echo "Deploying to Staging..."
    just _git-update staging
    just push-secrets staging
    just _build-and-deploy-staging

# SMART Deploy to Production (Auto-targets Inactive)
deploy-prod:
    @TARGET=$(./scripts/dev/get-inactive-color.sh {{vps_host}}) && \
    echo "Active is currently the OTHER one. Deploying to $TARGET..." && \
    just _deploy-prod-target $TARGET

# Internal helper (Do not run directly)
_deploy-prod-target color:
    @echo "Deploying to Production {{color}}..."
    just _git-update prod
    just push-secrets prod
    just _build-and-deploy-prod {{color}}

# --- Internal Deployment Steps ---

_git-update folder:
    ssh -t {{nuc_host}} "cd {{nuc_root}}/{{folder}} && git pull"

_build-and-deploy-staging:
    ssh -t {{nuc_host}} "cd {{nuc_root}}/staging && \
        SHA=\$(git rev-parse --short HEAD) && \
        ./ops/scripts/prem/generate-site.sh && \
        cp ops/docker-compose.yml ../docker/ && \
        docker build -t selfpraxis:staging-\$SHA . && \
        STAGING_TAG=\$SHA docker compose -f {{nuc_root}}/docker/docker-compose.yml up -d staging"

_build-and-deploy-prod color:
    ssh -t {{nuc_host}} "cd {{nuc_root}}/prod && \
        SHA=\$(git rev-parse --short HEAD) && \
        ./ops/scripts/prem/generate-site.sh && \
        cp ops/docker-compose.yml ../docker/ && \
        docker build -t selfpraxis:prod-\$SHA . && \
        PROD_TAG=\$SHA docker compose -f {{nuc_root}}/docker/docker-compose.yml up -d prod_{{color}}"

# Promote the Inactive Color to LIVE
promote:
    @TARGET=$(./scripts/dev/get-inactive-color.sh {{vps_host}}) && \
    echo "Promoting $TARGET to LIVE..." && \
    if [ "$TARGET" == "green" ]; then TARGET_PORT="8002"; else TARGET_PORT="8000"; fi && \
    just _flip-traffic $TARGET_PORT

# Internal helper
_flip-traffic port:
    @echo "Flipping upstream to port {{port}} on {{vps_host}}..."
    ssh -t {{vps_host}} "echo 'upstream selfpraxis_prod_active { server 100.104.75.126:{{port}}; }' | sudo tee {{vps_upstream_conf}} && sudo nginx -t && sudo systemctl reload nginx"

# Bootstrap the Nuc (Run once)
bootstrap-nuc:
    @echo "Bootstrapping Nuc directories and repos..."
    ssh -t {{nuc_host}} "sudo mkdir -p {{nuc_root}} && sudo chown \$(whoami) {{nuc_root}} && \
    cd {{nuc_root}} && \
    ([ -d staging ] || git clone https://github.com/SiderealMollusk/self-praxis.help.git staging) && \
    ([ -d prod ] || git clone https://github.com/SiderealMollusk/self-praxis.help.git prod) && \
    mkdir -p docker && echo 'Bootstrap complete!'"
