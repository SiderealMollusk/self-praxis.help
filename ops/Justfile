# Remote Hosts (Override these in .env)
# default: assuming you have ssh config aliases set up
set dotenv-load

nuc_host := env_var_or_default("nuc_host", "nuc")
vps_host := env_var_or_default("vps_host", "vps")

# Project Paths on Remote
nuc_root := "/srv/selfpraxis"
vps_upstream_conf := "/etc/nginx/snippets/selfpraxis-prod-upstream.conf"

set shell := ["bash", "-c"]

# Default: List commands
default:
    @just --list --unsorted

# Run Security Audit against live site
audit:
    @scripts/prem/audit.sh





# --- Secrets Management ---

# Encrypt ALL local .env files to secrets.enc.env
save-secrets:
    sops --encrypt ops/.env > ops/secrets.enc.env
    sops --encrypt apps/codenames/.env > apps/codenames/secrets.enc.env
    sops --encrypt apps/series-bible/.env > apps/series-bible/secrets.enc.env
    sops --encrypt apps/shift-signups/.env > apps/shift-signups/secrets.enc.env

# Decrypt ALL secrets.enc.env to local .env
load-secrets:
    sops --decrypt ops/secrets.enc.env > ops/.env
    sops --decrypt apps/codenames/secrets.enc.env > apps/codenames/.env
    sops --decrypt apps/series-bible/secrets.enc.env > apps/series-bible/.env
    sops --decrypt apps/shift-signups/secrets.enc.env > apps/shift-signups/.env

# --- App Deployment Workflows ---

# Deploy an App to Staging (Usage: just deploy codenames)
deploy app:
    @echo "Deploying {{app}}..."
    just _git-update
    just _push-app-config {{app}}
    just _build-and-deploy {{app}}

# Deploy an App to Staging (Usage: just deploy-staging codenames)
deploy-staging app:
    @echo "Deploying {{app}} to STAGING..."
    just _git-update
    just _push-app-config {{app}}
    just _build-and-deploy-staging {{app}}

_push-app-config app:
    @echo "Pushing {{app}} config..."
    scp ../apps/{{app}}/.env {{nuc_host}}:{{nuc_root}}/apps/{{app}}/.env

_git-update:
    ssh -t {{nuc_host}} "cd {{nuc_root}} && git pull"

_build-and-deploy-staging app:
    ssh -t {{nuc_host}} "cd {{nuc_root}} && \
        SHA=\$(git rev-parse --short HEAD) && \
        ./ops/scripts/prem/generate-site.sh {{app}} && \
        cp ops/docker-compose.yml docker/ && \
        docker build -t selfpraxis:{{app}}-staging-\$SHA apps/{{app}} && \
        STAGING_IMAGE=selfpraxis:{{app}}-staging-\$SHA docker compose -f docker/docker-compose.yml up -d staging"

_build-and-deploy app:
    ssh -t {{nuc_host}} "cd {{nuc_root}} && \
        SHA=\$(git rev-parse --short HEAD) && \
        ./ops/scripts/prem/generate-site.sh {{app}} && \
        cp ops/docker-compose.yml docker/ && \
        docker build -t selfpraxis:{{app}}-\$SHA apps/{{app}} && \
        TAG_VAR=\$(echo {{app}} | tr '[:lower:]' '[:upper:]' | tr '-' '_')_TAG && \
        export \${TAG_VAR}=\$SHA && \
        docker compose -f docker/docker-compose.yml up -d {{app}}"

# --- VPS Configuration ---

# Deploy Nginx Config to VPS
deploy-vps-nginx:
    @echo "Deploying Nginx config to {{vps_host}}..."
    scp nginx/prod-selfpraxis.conf {{vps_host}}:/tmp/prod-selfpraxis.conf
    ssh -t {{vps_host}} "sudo mv /tmp/prod-selfpraxis.conf /etc/nginx/sites-available/prod-selfpraxis && sudo nginx -t && sudo systemctl reload nginx"

# Harden VPS Security (Run once)
harden-vps:
    @echo "Hardening VPS {{vps_host}}..."
    scp scripts/vps/harden.sh {{vps_host}}:/tmp/harden.sh
    ssh -t {{vps_host}} "chmod +x /tmp/harden.sh && sudo /tmp/harden.sh && rm /tmp/harden.sh"

# Bootstrap the Nuc (Run once, safe to re-run)
bootstrap-nuc:
    @echo "Bootstrapping Nuc directories and repos..."
    ssh -t {{nuc_host}} "sudo mkdir -p {{nuc_root}} && sudo chown \$(whoami) {{nuc_root}} && \
    cd {{nuc_root}} && \
    ([ -d .git ] && git pull || git clone https://github.com/SiderealMollusk/self-praxis.help.git .) && \
    mkdir -p docker && \
    mkdir -p data/shift-signups && sudo chown -R 1000:1000 data/shift-signups && \
    echo 'Bootstrap complete!'"
